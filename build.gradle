buildscript {
    ext.kotlin_version = '1.1.51'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

def GROUP_ID = 'co.zsmb'
def ARTIFACT_ID = 'kotlinverbalexpressions'
def VERSION = '0.1'

group GROUP_ID
version VERSION

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

apply from: 'testformatter.gradle'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('bintrayUser')
    key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : System.getenv('bintrayKey')
    publications = ['mavenJava']

    pkg {
        repo = 'KotlinVerbalExpressions'
        name = 'KotlinVerbalExpressions'

        version {
            name = VERSION
            released = new Date()
        }
    }
}

task sourcesJar(type: Jar, dependsOn: project.classes) {
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId GROUP_ID
            artifactId ARTIFACT_ID
            version VERSION

            from components.java

            artifact sourcesJar {
                classifier = 'sources'
            }
        }
    }
}
